plugins { id 'org.beryx.runtime' version '1.12.7' }
apply plugin: 'application'

sourceCompatibility = 1.8
targetCompatibility = 1.8
sourceSets.main.java.srcDirs = [ "src" ]
sourceSets.main.resources.srcDirs = [ '../assets', '../../server/assets', "res"]

mainClassName = "com.argentumjk.client.DesktopLauncher"
eclipse.project.name = appName + "-desktop"
project.ext.assetsDir = new File("../assets")

import org.gradle.internal.os.OperatingSystem

def currentOs = OperatingSystem.current()

run {
    setClasspath(sourceSets.main.runtimeClasspath)
    setStandardInput(System.in)
    setWorkingDir(rootProject.file('assets').path)
    setIgnoreExitValue(true)

    if (currentOs.isMacOsX()) {
        // Required to run on macOS
        jvmArgs += "-XstartOnFirstThread"
    }
}

jar {
    setArchivesBaseName(appName)
    dependsOn configurations.runtimeClasspath
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    doLast {
        file(archiveFile).setExecutable(true, false)
    }
}

runtime {
    addOptions('--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages',
            '--strip-native-commands',
            '--vm', 'server')

    addModules('java.base' ,
            'java.desktop',
            'jdk.unsupported')

    jpackage {
        imageName = 'Argentum Online'
        installerName = imageName
        def imageType = currentOs.isWindows() ? 'ico' : currentOs.isMacOsX() ? 'icns' : 'png'
        imageOptions += ['--icon', file("./res/packaging/icon.$imageType")]

        if(currentOs.isWindows()) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            installerType = "exe"
        }
    }
}
