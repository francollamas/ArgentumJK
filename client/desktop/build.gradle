plugins { id 'org.beryx.runtime' version '1.12.4' }
apply plugin: 'application'

sourceCompatibility = 15
targetCompatibility = 15

sourceSets.main.java.srcDirs = [ "src" ]
sourceSets.main.resources.srcDirs = [ '../assets', "res"]

mainClassName = "com.argentumjk.client.desktop.DesktopLauncher"
eclipse.project.name = appName + "-desktop"
project.ext.assetsDir = new File("../assets")

def currentOs = org.gradle.internal.os.OperatingSystem.current()

run {
    workingDir = rootProject.file('assets').path
    setIgnoreExitValue(true)

    if (currentOs.macOsX) {
        // Required to run LWJGL3 Java apps on MacOS
        jvmArgs += "-XstartOnFirstThread"
    }
}

jar {
    setArchivesBaseName(appName)
    dependsOn configurations.runtimeClasspath
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    doLast {
        file(archiveFile).setExecutable(true, false)
    }
}

runtime {
    addOptions('--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages',
            '--strip-native-commands',
            '--vm', 'server')

    addModules('java.base' ,
            'java.desktop',
            'jdk.unsupported')

    jpackage {
        imageName = 'Argentum Online'
        installerName = imageName
        def imageType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        imageOptions += ['--icon', file("./res/packaging/icon.$imageType")]

        if(currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            installerType = "exe"
        }
    }
}
